apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

description = "kafka-connect-s3"

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
	}
}

allprojects  {
    apply plugin: 'maven'
    apply plugin: 'idea'

    group = 'com.spredfast.kafka.connect.s3'
	version = '0.2.0'

	apply plugin: 'java'
	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		maven { url "http://repo.maven.apache.org/maven2" }
	}
}

apply plugin: "com.github.johnrengelman.shadow"

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from subprojects*.tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
	from subprojects*.sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives javadocJar, sourcesJar
}



ext."signing.keyId" = '3D5954DE'
ext."signing.password" = System.env.KEY_PASS
ext."signing.secretKeyRingFile" = "${rootDir}/secring"

signing {
	sign configurations.archives
}

shadowJar {
	dependencies {
		// provided in the connect classpath
		exclude(dependency('org.apache.kafka:connect-api:0.10.0.1'))
		exclude(dependency('org.apache.kafka:kafka-clients:0.10.0.1'))
		exclude(dependency('net.jpountz.lz4:.*:.*'))
		exclude(dependency('org.xerial.snappy:.*:.*'))
		exclude(dependency('org.slf4j:.*:.*'))
	}

	// for things we directly depend on, repackage so we don't conflict with other connectors
	relocate 'com.amazonaws', 'com.spredfast.shade.amazonaws'
	relocate 'com.fasterxml', 'com.spredfast.shade.fasterxml'
	relocate 'org.apache', 'com.spredfast.shade.apache'
	relocate 'org.joda', 'com.spredfast.shade.joda'
}

dependencies {
	compile project(':sink')
	compile project(':source')
}

artifacts {
	archives(tasks.shadowJar) {
		classifier 'shadow'
		builtBy tasks.shadowJar
	}
}

def ossrhUsername = System.env.OSSRH_USER
def ossrhPassword = System.env.OSSRH_PASS

def mavenConfig = {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'Kafka Connect S3'
				packaging 'jar'
				// optionally artifactId can be defined here
				description 'S3 Source and Sink Connectors for Kafka Connect'
				url 'https://github.com/spredfast/kafka-connect-s3'

				scm {
					connection 'scm:git:https://github.com/spredfast/kafka-connect-s3.git'
					developerConnection 'scm:git:https://github.com/spredfast/kafka-connect-s3.git'
					url 'https://github.com/spredfast/kafka-connect-s3'
				}

				licenses {
					license {
						name 'BSD License'
						url 'https://github.com/spredfast/kafka-connect-s3/blob/master/LICENSE.md'
					}
				}

				developers {
					developer {
						id 'iamnoah'
						name 'Noah Sloan'
						email 'nsloan@spredfast.com'
					}
				}
			}
		}
	}
}

uploadArchives mavenConfig
uploadShadow mavenConfig
